# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=php54-xcache
pkgver=3.2.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A PHP opcode cacher'
url='http://xcache.lighttpd.net/'
depends=('php54')
license=('custom')
source=("http://xcache.lighttpd.net/pub/Releases/${pkgver}/xcache-${pkgver}.tar.bz2"
        'xcache.ini')
backup=('etc/php54/conf.d/xcache.ini')

build() {
	cd "$srcdir/xcache-$pkgver"
	/opt/php54/bin/phpize
	./configure --with-php-config=/opt/php54/bin/php-config
	make
}

package() {
	cd "$srcdir/xcache-$pkgver"
	make INSTALL_ROOT="$pkgdir" install

	install -Dm644 "$srcdir/xcache.ini" "$pkgdir/etc/php54/conf.d/xcache.ini"

	install -dm755 "$pkgdir/usr/share/$pkgname/admin"
	cp -r htdocs/* "$pkgdir/usr/share/$pkgname/admin/"

	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('7d12a3cbd5f729b70f9226ac19406d11219f1c5f5e82b3bbb43ea9460cbf6cbd'
            'c8c843335028b1da1965ff71b73b42fc4bc45be08bf9ec3f71aff5744a428176')
