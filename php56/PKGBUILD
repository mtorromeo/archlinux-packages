# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=php56
pkgname=('php56'
         'php56-cgi'
         'php56-apache'
         'php56-fpm'
         'php56-embed'
         'php56-pear'
         'php56-enchant'
         'php56-gd'
         'php56-imap'
         'php56-intl'
         'php56-ldap'
         'php56-mcrypt'
         'php56-mssql'
         'php56-odbc'
         'php56-dblib'
         'php56-pgsql'
         'php56-pspell'
         'php56-snmp'
         'php56-sqlite'
         'php56-tidy'
         'php56-xsl')
pkgver=5.6.21
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('apache' 'c-client' 'postgresql-libs' 'libldap' 'postfix'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'enchant' 'file' 'freetds'
             'libmcrypt' 'tidy' 'aspell' 'libltdl' 'gd' 'icu'
             'curl' 'libxslt' 'openssl' 'db' 'gmp' 'systemd')
source=("http://php.net/distributions/${pkgbase%56}-${pkgver}.tar.xz"
        'php.ini.patch' 'apache.conf' 'php-fpm.conf.in.patch'
        'logrotate.d.php-fpm' 'php-fpm.service' 'php-fpm.tmpfiles')

prepare() {
	cd ${srcdir}/${pkgbase%56}-${pkgver}

	# fix man pages (bug #66842)
	# sed '/.1 | xargs rm -f$/d' -i Makefile.global

	# adjust paths
	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch

	# workaround for pkg-config 0.28
	sed -e 's/ && test -n "$OPENSSL_INCS"//g' \
		-i configure

	# Just because our Apache 2.4 is configured with a threaded MPM by default does not mean we want to build a ZTS PHP.
	# Let's supress this behaviour and build a SAPI that works fine with the prefork MPM.
	sed '/APACHE_THREADED_MPM=/d' -i sapi/apache2handler/config.m4 -i configure
}

build() {
	local _phpconfig="--srcdir=../${pkgbase%56}-${pkgver} \
		--config-cache \
		--prefix=/opt/php56 \
		--sysconfdir=/etc/php56 \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php56 \
		--with-config-file-scan-dir=/etc/php56/conf.d \
		--enable-inline-optimization \
		--disable-debug \
		--disable-rpath \
		--disable-static \
		--enable-shared \
		--mandir=/usr/share/man \
		--without-pear \
		"

	local _phpextensions="--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-opcache \
		--enable-phar=shared \
		--enable-posix=shared \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-fpm-systemd \
		--with-freetype-dir=/usr \
		--with-xpm-dir=/usr \
		--with-gd=shared,/usr \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl \
		--with-imap=shared \
		--with-kerberos=/usr \
		--with-jpeg-dir=/usr \
		--with-vpx-dir=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-libzip \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mssql=shared \
		--with-mysql-sock=/run/mysqld/mysqld.sock \
		--with-mysql=shared,mysqlnd \
		--with-mysqli=shared,mysqlnd \
		--with-openssl=shared \
		--with-pcre-regex=/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pdo-dblib=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=/usr \
		--with-pspell=shared \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		"

	export EXTENSION_DIR=/opt/php56/modules
	export PEAR_INSTALLDIR=/opt/php56/pear

	msg2 "Building CLI"
	cd "$srcdir"
	rm -rf build-php
	mkdir build-php
	cd build-php
	ln -s ../${pkgbase%56}-${pkgver}/configure
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		${_phpextensions}
	make

	msg2 "Building cgi/fcgi API"
	cd "$srcdir"
	rm -rf build-cgi
	cp -a build-php build-cgi
	cd build-cgi
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-cgi \
		${_phpextensions}
	make

	msg2 "Building apache API"
	cp -a ${srcdir}/build-php ${srcdir}/build-apache
	cd ${srcdir}/build-apache
	./configure ${_phpconfig} \
		--disable-cli \
		--with-apxs2 \
		${_phpextensions}
	make

	msg2 "Building FPM API"
	cd "$srcdir"
	rm -rf build-fpm
	cp -a build-php build-fpm
	cd build-fpm
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-fpm \
		--with-fpm-user=http \
		--with-fpm-group=http \
		${_phpextensions}
	make

	msg2 "Building Embed"
	# embed
	cp -a ${srcdir}/build-php ${srcdir}/build-embed
	cd ${srcdir}/build-embed
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-embed=shared \
		${_phpextensions}
	make

	msg2 "Building PEAR"
	cd "$srcdir"
	rm -rf build-pear
	cp -a build-php build-pear
	cd build-pear
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		--with-pear \
		${_phpextensions}
	make
}

# check() {
# 	cd ${srcdir}/build-php
# 	make test
# }

package_php56() {
	pkgdesc='An HTML-embedded scripting language'
	provides=("php=$pkgver")
	depends=('pcre' 'libxml2' 'bzip2' 'curl')
	backup=('etc/php56/php.ini')
	replaces=('php55')

	cd ${srcdir}/build-php
	make -j1 INSTALL_ROOT=${pkgdir} install

	# install php.ini
	install -D -m644 ${srcdir}/${pkgbase%56}-${pkgver}/php.ini-production ${pkgdir}/etc/php56/php.ini
	install -d -m755 ${pkgdir}/etc/php56/conf.d/

	# links
	install -dm755 "$pkgdir/usr/bin"
	for bin in php phar; do
		ln -s /opt/php56/bin/$bin "$pkgdir/usr/bin/${bin}55"
	done

	# remove conflicting files
	rm -rf ${pkgdir}/usr/share/man

	# remove static modules
	rm -f ${pkgdir}/opt/php56/modules/*.a
	# remove modules provided by sub packages
	rm -f ${pkgdir}/opt/php56/modules/{enchant,gd,imap,intl,ldap,mcrypt,mssql,odbc,pdo_dblib,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
}

package_php56-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	depends=('php56')
	replaces=('php55-cgi')

	install -D -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}/opt/php56/bin/php-cgi
}

package_php56-apache() {
	pkgdesc='Apache SAPI for PHP'
# 	conflicts="php-apache"
	depends=('php56' 'apache')
	replaces=('php55-apache')
	backup=('etc/httpd/conf/extra/php5_module.conf')

	install -D -m755 ${srcdir}/build-apache/libs/libphp5.so ${pkgdir}/usr/lib/httpd/modules/libphp5.so
	install -D -m644 ${srcdir}/apache.conf ${pkgdir}/etc/httpd/conf/extra/php5_module.conf
}

package_php56-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php56' 'systemd')
	replaces=('php55-fpm')
	backup=('etc/php56/php-fpm.conf')
	install='php-fpm.install'

	install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}/opt/php56/bin/php-fpm
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}/etc/php56/php-fpm.conf
	install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}/etc/logrotate.d/php56-fpm
	install -d -m755 ${pkgdir}/etc/php56/fpm.d
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php56-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php56-fpm.service
}

package_php56-embed() {
	pkgdesc='Embed SAPI for PHP'
	depends=('php56')
	replaces=('php55-embed')

	install -D -m755 ${srcdir}/build-embed/libs/libphp5.so ${pkgdir}/opt/php56/lib/libphp5.so
	install -D -m644 ${srcdir}/php-${pkgver}/sapi/embed/php_embed.h ${pkgdir}/opt/php56/include/php/sapi/embed/php_embed.h
}

package_php56-pear() {
	pkgdesc='PHP Extension and Application Repository'
	depends=('php56')
	replaces=('php55-pear')
	backup=('etc/php56/pear.conf')

	cd ${srcdir}/build-pear
	make -j1 install-pear INSTALL_ROOT=${pkgdir}
	local i
	while read i; do
		[ ! -e "$i" ] || rm -rf "$i"
	done < <(find ${pkgdir} -name '.*')

	# links
	install -dm755 "$pkgdir/usr/bin"
	for bin in pear pecl; do
		ln -s /opt/php56/bin/$bin "$pkgdir/usr/bin/${bin}55"
	done
}

package_php56-enchant() {
	depends=('php56' 'enchant')
	replaces=('php55-enchant')
	pkgdesc='enchant module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/enchant.so ${pkgdir}/opt/php56/modules/enchant.so
}

package_php56-gd() {
	depends=('php56' 'libpng' 'libjpeg' 'freetype2')
	replaces=('php55-gd')
	pkgdesc='gd module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}/opt/php56/modules/gd.so
}

package_php56-imap() {
	depends=('php56' 'c-client')
	pkgdesc='imap module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/imap.so ${pkgdir}/opt/php56/modules/imap.so
}

package_php56-intl() {
	depends=('php56' 'icu')
	replaces=('php55-intl')
	pkgdesc='intl module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}/opt/php56/modules/intl.so
}

package_php56-ldap() {
	depends=('php56' 'libldap')
	replaces=('php55-ldap')
	pkgdesc='ldap module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/ldap.so ${pkgdir}/opt/php56/modules/ldap.so
}

package_php56-mcrypt() {
	depends=('php56' 'libmcrypt' 'libltdl')
	replaces=('php55-mcrypt')
	pkgdesc='mcrypt module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}/opt/php56/modules/mcrypt.so
}

package_php56-mssql() {
	depends=('php56' 'freetds')
	replaces=('php55-mssql')
	pkgdesc='mssql module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/mssql.so ${pkgdir}/opt/php56/modules/mssql.so
}

package_php56-odbc() {
	depends=('php56' 'unixodbc')
	replaces=('php55-odbc')
	pkgdesc='ODBC modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}/opt/php56/modules/odbc.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}/opt/php56/modules/pdo_odbc.so
}

package_php56-dblib() {
	depends=('php56' 'freetds')
	replaces=('php55-dblib')
	pkgdesc='DBLIB modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pdo_dblib.so ${pkgdir}/opt/php56/modules/pdo_dblib.so
}

package_php56-pgsql() {
	depends=('php56' 'postgresql-libs')
	replaces=('php55-pgsql')
	pkgdesc='PostgreSQL modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}/opt/php56/modules/pgsql.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}/opt/php56/modules/pdo_pgsql.so
}

package_php56-pspell() {
	depends=('php56' 'aspell')
	replaces=('php55-pspell')
	pkgdesc='pspell module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}/opt/php56/modules/pspell.so
}

package_php56-snmp() {
	depends=('php56' 'net-snmp')
	replaces=('php55-snmp')
	pkgdesc='snmp module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}/opt/php56/modules/snmp.so
}

package_php56-sqlite() {
	depends=('php56' 'sqlite')
	replaces=('php55-sqlite')
	pkgdesc='sqlite module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}/opt/php56/modules/sqlite3.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}/opt/php56/modules/pdo_sqlite.so
}

package_php56-tidy() {
	depends=('php56' 'tidy')
	replaces=('php55-tidy')
	pkgdesc='tidy module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}/opt/php56/modules/tidy.so
}

package_php56-xsl() {
	depends=('php56' 'libxslt')
	replaces=('php55-xsl')
	pkgdesc='xsl module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}/opt/php56/modules/xsl.so
}

sha256sums=('566ff1a486cb0485ed477a91ea292423f77a58671270ff73b74e67e3ce7084f9'
            '89fb65860168a9b04b312c1762db7f68ce4a2ebee91d83f7a8c24c3432969fb6'
            '6ee6ed48b8416390c56b2e32b75c79f85586431b5c44c48ed22ae6a8dbe68332'
            '59f91e8374da3dc1b9fcf5a737aed245ea769f057a3d3542e08274ece2743479'
            '8d926589d89f919973d001d82631d64286c3ddc89cd1182f1709afa3908b223e'
            '1e5f2f00d5e8cf2034d63374a2be123c532b758340dd411f741bbee784ce0daa'
            '640dba0d960bfeaae9ad38d2826d3f6b5d6c175a4d3e16664eefff29141faad5')
