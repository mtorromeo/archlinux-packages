# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=tshock
epoch=1
pkgver=4.3.6
_pkgver=b7fedc173b5c44e6e00dba3481406debb26e23f2
#_pkgver=v$pkgver
_apiver=7f29c87f6451fe6e68c4cde4ff792e7234b809cf
pkgrel=2
pkgdesc="A Terraria Server administration modification that runs on the open source TerrariaAPI."
arch=(any)
url="http://tshock.co/xf/"
license=("GPL")
depends=('tmux' 'mono')
source=("https://github.com/NyxStudios/TShock/archive/$_pkgver/$pkgname-$_pkgver.tar.gz"
        "https://github.com/NyxStudios/TerrariaAPI-Server/archive/$_apiver/TerrariaAPI-Server-$_apiver.tar.gz"
        'tshock.sh'
        'tshock.service')

sha256sums=('88a58d70ba07aa5304edf75bd136eb3b427ccaf8ceab64314b54d5944eff6a65'
            '2a19794c27c5f7b3bbf86c9d932a2d4152ed42c0594206f40c17dd6904b77f51'
            '2545482d79ded5e503be03beb75495dcbbefe8f97043d5669f0dbbc62306e8d0'
            '4ff47e2fad25379a613fe874bf3e9a3e6830c874d3df9e19824ef763b829d939')

install=tshock.install

build() {
  cd TShock-${_pkgver#v}
  rm -rf TerrariaServerAPI
  ln -s ../TerrariaAPI-Server-$_apiver TerrariaServerAPI
  xbuild TShockAPI/TShockAPI.csproj /p:Configuration=Release
}

package() {
  install -Dm644 tshock.service "$pkgdir"/usr/lib/systemd/system/tshock.service
  install -Dm755 tshock.sh "$pkgdir"/usr/bin/tshock

  cd TShock-${_pkgver#v}

  install -dm755 "$pkgdir"/opt/tshock/ServerPlugins
  install -dm755 "$pkgdir"/var/log/tshock

  cp -a TShockAPI/bin/Release/*.dll "$pkgdir"/opt/tshock/ServerPlugins/
  cp -a TShockAPI/bin/Release/*.exe "$pkgdir"/opt/tshock/

  install -Dm644 prebuilts/sqlite3.dll "$pkgdir"/opt/tshock/sqlite3.dll

  cd "$pkgdir"/opt/tshock
  mv ServerPlugins/Newtonsoft.Json.dll .
  ln -s "My Games/Terraria" Terraria
}
