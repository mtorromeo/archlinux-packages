# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=tshock
epoch=1
pkgver=4.3.9
#_pkgver=b7fedc173b5c44e6e00dba3481406debb26e23f2
_pkgver=v$pkgver
_apiver=24c7c53fc91e3d34f81d6320b064f552679e0d62
pkgrel=1
pkgdesc="A Terraria Server administration modification that runs on the open source TerrariaAPI."
arch=(any)
url="http://tshock.co/xf/"
license=("GPL")
depends=('tmux' 'mono')
source=("https://github.com/NyxStudios/TShock/archive/$_pkgver/$pkgname-$_pkgver.tar.gz"
        "https://github.com/NyxStudios/TerrariaAPI-Server/archive/$_apiver/TerrariaAPI-Server-$_apiver.tar.gz"
        'tshock.sh'
        'tshock.service')

sha256sums=('733bf91682b982ab2f2cfb7e0fea794c74a067bad660eabd9ed8cca089298ffe'
            '6ba5a720e5ebe551ce9c128f426611a72baf5e69fd605c80662f97aa56fce0ec'
            '2545482d79ded5e503be03beb75495dcbbefe8f97043d5669f0dbbc62306e8d0'
            'e1a9a272f5a9674fd045d88c2651135c4d0db1a41433934e668143017a76043d')

install=tshock.install

build() {
  cd TShock-${_pkgver#v}
  rm -rf TerrariaServerAPI
  ln -s ../TerrariaAPI-Server-$_apiver TerrariaServerAPI
  xbuild TShockAPI/TShockAPI.csproj /p:Configuration=Release
}

package() {
  install -Dm644 tshock.service "$pkgdir"/usr/lib/systemd/system/tshock.service
  install -Dm755 tshock.sh "$pkgdir"/usr/bin/tshock

  cd TShock-${_pkgver#v}

  install -dm755 "$pkgdir"/opt/tshock/ServerPlugins
  install -dm755 "$pkgdir"/var/log/tshock

  cp -a TShockAPI/bin/Release/*.dll "$pkgdir"/opt/tshock/ServerPlugins/
  cp -a TShockAPI/bin/Release/*.exe "$pkgdir"/opt/tshock/

  install -Dm644 prebuilts/sqlite3.dll "$pkgdir"/opt/tshock/sqlite3.dll

  cd "$pkgdir"/opt/tshock
  mv ServerPlugins/Newtonsoft.Json.dll .
  ln -s "My Games/Terraria" Terraria
}
