# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=tshock
epoch=1
pkgver=4.3.5
_pkgver=f6f2ebdd96f6acde4a4b3d42970c66d8cfdbc33d
_apiver=fe600ac0d9d799e4b22d95983cc9dbd1669346c5
pkgrel=2
pkgdesc="A Terraria Server administration modification that runs on the open source TerrariaAPI."
arch=(any)
url="http://tshock.co/xf/"
license=("GPL")
depends=('tmux' 'mono')
source=("https://github.com/NyxStudios/TShock/archive/$_pkgver/$pkgname-$_pkgver.tar.gz"
        "https://github.com/NyxStudios/TerrariaAPI-Server/archive/$_apiver/TerrariaAPI-Server-$_apiver.tar.gz"
        'tshock.sh'
        'tshock.service')

sha256sums=('7371a25a84bae461bd219584e643e752c6db2dd5c0fd2c6d332e95129b3cd887'
            'fba4c288677fd7d5d6a02038287418304d3f736add50750c6f55456c709cef4b'
            '2545482d79ded5e503be03beb75495dcbbefe8f97043d5669f0dbbc62306e8d0'
            '298244a08c1828067d11f490d5668df82987f0ceec604114fc832cb585a7b328')

install=tshock.install

build() {
  cd TShock-$_pkgver
  rm -rf TerrariaServerAPI
  ln -s ../TerrariaAPI-Server-$_apiver TerrariaServerAPI
  xbuild TShockAPI/TShockAPI.csproj /p:Configuration=Release
}

package() {
  install -d "$pkgdir"/opt
  cp -a TShock-$_pkgver/TShockAPI/bin/Release "$pkgdir"/opt/tshock
  install -d "$pkgdir"/opt/tshock/ServerPlugins
  install -Dm644 tshock.service "$pkgdir"/usr/lib/systemd/system/tshock.service

  install -Dm755 tshock.sh "$pkgdir"/usr/bin/tshock

  cd "$pkgdir"/opt/tshock
  ln -s "My Games/Terraria" Terraria
}
