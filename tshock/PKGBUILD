# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=tshock
epoch=1
pkgver=4.3.8
#_pkgver=b7fedc173b5c44e6e00dba3481406debb26e23f2
_pkgver=v$pkgver
_apiver=c5e564249e7e424a3acf08d3be5584af2b0f4032
pkgrel=1
pkgdesc="A Terraria Server administration modification that runs on the open source TerrariaAPI."
arch=(any)
url="http://tshock.co/xf/"
license=("GPL")
depends=('tmux' 'mono')
source=("https://github.com/NyxStudios/TShock/archive/$_pkgver/$pkgname-$_pkgver.tar.gz"
        "https://github.com/NyxStudios/TerrariaAPI-Server/archive/$_apiver/TerrariaAPI-Server-$_apiver.tar.gz"
        'tshock.sh'
        'tshock.service')

sha256sums=('3bd460879a89d554272e1fc2bdbe26efc674f211815c71f5d4e9d1e1233b3798'
            'e34b3d71f61b9f3b1a5d18c4f0b2cea69af8f4fa1e5266010dd8084ca123f1c0'
            '2545482d79ded5e503be03beb75495dcbbefe8f97043d5669f0dbbc62306e8d0'
            'e1a9a272f5a9674fd045d88c2651135c4d0db1a41433934e668143017a76043d')

install=tshock.install

build() {
  cd TShock-${_pkgver#v}
  rm -rf TerrariaServerAPI
  ln -s ../TerrariaAPI-Server-$_apiver TerrariaServerAPI
  xbuild TShockAPI/TShockAPI.csproj /p:Configuration=Release
}

package() {
  install -Dm644 tshock.service "$pkgdir"/usr/lib/systemd/system/tshock.service
  install -Dm755 tshock.sh "$pkgdir"/usr/bin/tshock

  cd TShock-${_pkgver#v}

  install -dm755 "$pkgdir"/opt/tshock/ServerPlugins
  install -dm755 "$pkgdir"/var/log/tshock

  cp -a TShockAPI/bin/Release/*.dll "$pkgdir"/opt/tshock/ServerPlugins/
  cp -a TShockAPI/bin/Release/*.exe "$pkgdir"/opt/tshock/

  install -Dm644 prebuilts/sqlite3.dll "$pkgdir"/opt/tshock/sqlite3.dll

  cd "$pkgdir"/opt/tshock
  mv ServerPlugins/Newtonsoft.Json.dll .
  ln -s "My Games/Terraria" Terraria
}
