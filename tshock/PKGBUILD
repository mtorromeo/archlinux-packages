# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=tshock
epoch=1
pkgver=4.3.5
_apiver=e124c5a51cf4a418de47b7767b13669a379d1935
pkgrel=1
pkgdesc="A Terraria Server administration modification that runs on the open source TerrariaAPI."
arch=(any)
url="http://tshock.co/xf/"
license=("GPL")
depends=('tmux' 'mono')
source=("https://github.com/NyxStudios/TShock/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "https://github.com/NyxStudios/TerrariaAPI-Server/archive/$_apiver/TerrariaAPI-Server-$_apiver.tar.gz"
        'tshock.sh'
        'tshock.service')

sha256sums=('43ea6ece641a0775ca7fe80355986d513917858c8193b877dd65efb3010cc851'
            'c61aa8d4c91f12f0eef290713096e8987f00c380cdcf411a71beda701cdde24d'
            '2545482d79ded5e503be03beb75495dcbbefe8f97043d5669f0dbbc62306e8d0'
            '298244a08c1828067d11f490d5668df82987f0ceec604114fc832cb585a7b328')

install=tshock.install

build() {
  cd TShock-$pkgver
  rm -rf TerrariaServerAPI
  ln -s ../TerrariaAPI-Server-$_apiver TerrariaServerAPI
  xbuild TShockAPI/TShockAPI.csproj /p:Configuration=Release
}

package() {
  install -d "$pkgdir"/opt
  cp -a TShock-$pkgver/TShockAPI/bin/Release "$pkgdir"/opt/tshock
  install -d "$pkgdir"/opt/tshock/ServerPlugins
  install -Dm644 tshock.service "$pkgdir"/usr/lib/systemd/system/tshock.service

  install -Dm755 tshock.sh "$pkgdir"/usr/bin/tshock

  cd "$pkgdir"/opt/tshock
  ln -s "My Games/Terraria" Terraria
}
