# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname="php-pdo_dblib"
pkgver=7.0.2
pkgrel=1
pkgdesc="pdo_dblib extension for PHP"
arch=('i686' 'x86_64')
url="http://php.net/manual/en/ref.pdo-dblib.php"
license=('PHP')
depends=('php' 'freetds')
makedepends=('php')
source=("http://php.net/distributions/php-$pkgver.tar.xz")
sha256sums=('556121271a34c442b48e3d7fa3d3bbb4413d91897abbb92aaeced4a7df5f2ab2')
backup=('etc/php/conf.d/pdo_dblib.ini')

build() {
  cd "$srcdir"/php-$pkgver/ext/pdo_dblib
  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/php-$pkgver/ext/pdo_dblib

  make INSTALL_ROOT="$pkgdir" install
  echo ";extension=pdo_dblib.so" > pdo_dblib.ini
  install -Dm644 pdo_dblib.ini "$pkgdir"/etc/php/conf.d/pdo_dblib.ini
}
