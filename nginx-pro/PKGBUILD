_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

pkgname=nginx-pro
pkgver=1.8.0
pkgrel=1

_uploadprogver=0.9.1
_uploadprogcommit=39e4d53

_pushstreamver=0.4.1
_pushstreamcommit=5be47b0

_davextver=0.0.3
_davextcommit=6974186

_headersmorever=0.26
_headersmorecommit=d20bf26

_subsfilterver=0.6.4
_cachepurgever=2.3
_modver=2.9.0

# openresty
_echover=0.57-1
_echocommit=a7365111f51fa40443f491f3c88f153727a259be

pkgdesc="Lightweight HTTP server with some additional modules (no mail modules)"
arch=('i686' 'x86_64')
depends=('libxml2' 'lua51' 'yajl' 'curl' 'apr-util' 'geoip')
makedepends=('git' 'apache')
url="http://nginx.org"
license=('custom')
conflicts=('nginx')
provides=('nginx')

backup=(${_cfgdir:1}/modsec-recommended.conf
        ${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/nginx)

source=(http://nginx.org/download/nginx-$pkgver.tar.gz
        modsecurity-$_modver.tar.gz::https://github.com/SpiderLabs/ModSecurity/archive/v$_modver.tar.gz
        nginx-upload-progress-$_uploadprogver.tgz::https://github.com/masterzen/nginx-upload-progress-module/tarball/v$_uploadprogver
        headers-more-nginx-module-$_headersmorever.tgz::https://github.com/openresty/headers-more-nginx-module/tarball/v$_headersmorever
        nginx-dav-ext-module-$_davextver.tgz::https://github.com/arut/nginx-dav-ext-module/tarball/v$_davextver
        nginx_cache_purge-$_cachepurgever.tar.gz::https://github.com/FRiCKLE/ngx_cache_purge/archive/$_cachepurgever.tar.gz
        nginx-push-stream-module-$_pushstreamver.tgz::https://github.com/wandenberg/nginx-push-stream-module/tarball/$_pushstreamver
        echo-nginx-module-$_echover.tar.gz::https://github.com/openresty/echo-nginx-module/archive/$_echocommit.tar.gz
        ngx_http_substitutions_filter_module-$_subsfilterver.tar.gz::https://github.com/yaoweibin/ngx_http_substitutions_filter_module/archive/v$_subsfilterver.tar.gz
        modsec-recommended.conf
        service
        nginx.logrotate)

build() {
	local _modules=(
		--add-module=../masterzen-nginx-upload-progress-module-$_uploadprogcommit
		--add-module=../ngx_cache_purge-$_cachepurgever
		--add-module=../arut-nginx-dav-ext-module-$_davextcommit
		--add-module=../wandenberg-nginx-push-stream-module-$_pushstreamcommit
		--add-module=../echo-nginx-module-$_echocommit
    --add-module=../openresty-headers-more-nginx-module-$_headersmorecommit
    --add-module=../ngx_http_substitutions_filter_module-$_subsfilterver
	)

	msg "Compiling mod-security"
	_modules=(${_modules[@]} --add-module=../ModSecurity-$_modver/nginx/modsecurity)
	cd "$srcdir/ModSecurity-$_modver"
	sh autogen.sh
 	CFLAGS="-g -O0" ./configure \
		--prefix=/usr \
		--enable-standalone-module \
		--disable-apache2-module \
		--enable-pcre-study \
		--enable-pcre-jit \
		--enable-pcre-match-limit=1000 \
		--enable-pcre-match-limit-recursion=1000 \
		--enable-lua-cache
	sed -r '/\$\$base/d' -i standalone/Makefile
 	make
 	make DESTDIR="$srcdir/ModSecurity-$_modver/install" install

	cd "$srcdir/nginx-$pkgver"

	msg "Building nginx"

	CFLAGS="-I /usr/include/httpd -I /usr/include/apr-1" ./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/bin/nginx \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --user=http --group=http \
        --http-log-path=/var/log/nginx/access.log \
        --error-log-path=stderr \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-imap --with-imap_ssl_module \
        --with-ipv6 --with-pcre-jit \
        --with-file-aio \
        --with-threads \
        --with-http_dav_module \
        --with-http_geoip_module \
        --with-http_gunzip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_spdy_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --with-http_addition_module \
        --with-http_degradation_module \
        --with-http_flv_module \
        --with-http_mp4_module \
        --with-http_secure_link_module \
        --with-http_sub_module \
        --without-mail_pop3_module --without-mail_imap_module --without-mail_smtp_module \
        ${_modules[@]} \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_random_index_module \
        #--with-http_perl_module \

	make
}

package() {
	cd "$srcdir"/nginx-$pkgver
	make DESTDIR="$pkgdir" install


	install -Dm644 contrib/vim/ftdetect/nginx.vim \
		"$pkgdir"/usr/share/vim/vimfiles/ftdetect/nginx.vim
	install -Dm644 contrib/vim/syntax/nginx.vim \
		"$pkgdir"/usr/share/vim/vimfiles/syntax/nginx.vim
	install -Dm644 contrib/vim/indent/nginx.vim \
		"$pkgdir"/usr/share/vim/vimfiles/indent/nginx.vim

	for bin in "$srcdir/ModSecurity-$_modver"/install/usr/bin/*; do
		binname=$(basename "$bin")
		install -Dm755 "$bin" "$pkgdir/usr/bin/$binname"
	done

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
	    -e '2s|worker_processes  1|worker_processes auto|' \
	    -e '44s|html|/usr/share/nginx/html|' \
	    -e '54s|html|/usr/share/nginx/html|' \
	    -i "$pkgdir"$_cfgdir/nginx.conf

	rm "$pkgdir"/etc/nginx/*.default

	install -d "$pkgdir"/var/lib/nginx
	install -dm700 "$pkgdir"/var/lib/nginx/proxy

	install -Dm644 "$srcdir/modsec-recommended.conf" "$pkgdir/$_cfgdir/modsec-recommended.conf"

	install -d "$pkgdir"/$_tmpdir
	install -dm700 -o http "$pkgdir"/$_tmpdir/{proxy,modsecurity/{suspicious,msa}}

	install -d "$pkgdir"/var/log
	install -dm750 -o http -g log "$pkgdir"/var/log/nginx/modsec

	install -d "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

	install -Dm644 "$srcdir"/nginx.logrotate "$pkgdir"/etc/logrotate.d/nginx
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
	install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	rmdir "$pkgdir"/var/run

	install -d "$pkgdir"/usr/share/man/man8/
	gzip -9c man/nginx.8 > "$pkgdir"/usr/share/man/man8/nginx.8.gz
}

sha256sums=('23cca1239990c818d8f6da118320c4979aadf5386deda691b1b7c2c96b9df3d5'
            '13d3cdc5350bb5c607659e4223d879674437b2357a77cdd2747d5dbfe9b531b8'
            'f6e2d867ab545eda2028799ac1af09bf0fd8814530cc387df5218cd8de6bbb18'
            '5ef76a714f4a0444f8c0ff12c50ca7cc2ba21c76799bd84d8e14fbfc0d43b646'
            '6f40a158be9f60918162cd1cdf4ab01c7ad2d07c94983e44240ce4201a4f07a0'
            'cb7d5f22919c613f1f03341a1aeb960965269302e9eb23425ccaabd2f5dcbbec'
            '6331c4b30742137c41ebab254f2c14045fb0739e7870ee964419c57a69a21045'
            'd88b2b909471a0343a1522b6c697c735bf3ba86c9b2dddea9334d920f6fdb6e5'
            'ed4ddbcf0c434f4a1e97b61251a63ace759792764bd5cb79ff20efe348db8db3'
            '838637cc4f5fd9d3209720db23234d99972e95549d8ac8eb619474f66376e8b1'
            '78631071af6aed03cca9c53778984ab4c010346102634ce3372246f439c76b82'
            '9523a1fdd5eb61bf62f3049f6ee088b198e36d5edcce2d9b08bbeb2930aa5a16')
