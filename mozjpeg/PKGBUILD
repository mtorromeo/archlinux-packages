# Maintainer: afontenot <adam.m.fontenot@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: N30N <archlinux@alunamation.com>

pkgname="mozjpeg"
pkgver=3.2
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline decoding and superior encoding"
url="https://github.com/mozilla/mozjpeg"
license=("BSD")
arch=("i686" "x86_64")
makedepends=("nasm")
source=("https://github.com/mozilla/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha1sums=('da2106543d82bfa51fb51e5d4b53397ac28ac87b')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	autoreconf -fiv
	./configure --prefix=/opt/mozjpeg --with-jpeg8 --mandir=/usr/share/man
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make \
		DESTDIR="$pkgdir" \
		docdir="/usr/share/doc/$pkgname" \
		exampledir="/usr/share/doc/$pkgname" \
		install

	rm -rf "$pkgdir/usr/share/man/man1/"

	install -d "$pkgdir/usr/share/licenses/$pkgname"
	ln -t "$pkgdir/usr/share/licenses/$pkgname" -s "../../doc/$pkgname/README"{,-turbo.txt,-mozilla.txt}
}
