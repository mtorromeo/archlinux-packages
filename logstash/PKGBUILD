# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=logstash
pkgver=1.5.0
pkgrel=1
pkgdesc='A tool for managing events and logs'
arch=('any')
url='http://logstash.net'
license=('Apache License')
depends=('java-runtime')
install=logstash.install

source=("https://download.elasticsearch.org/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
        "logstash.service"
        "logstash@.service"
        "logstash-web.service")

package() {
  cd "$srcdir"

  install -dm755 "$pkgdir"/opt
  cp -a logstash-$pkgver "$pkgdir"/opt/logstash

  install -Dm644 logstash.service "$pkgdir"/usr/lib/systemd/system/logstash.service
  install -Dm644 logstash@.service "$pkgdir"/usr/lib/systemd/system/logstash@.service
  install -Dm644 logstash-web.service "$pkgdir"/usr/lib/systemd/system/logstash-web.service

  install -dm755 "$pkgdir"/var/lib/logstash
  install -dm755 "$pkgdir"/etc/logstash/conf.d

  install -dm755 "$pkgdir"/usr/bin
  ln -s /opt/logstash/bin/logstash "$pkgdir"/usr/bin/logstash

  cd "$pkgdir"/opt/logstash/bin
  rm logstash.bat
  sed -r 's#^basedir=.*#basedir=/opt/logstash#' -i *

  sed -e '1i [ -f /etc/profile.d/jre.sh ] && . /etc/profile.d/jre.sh' \
      -e '2i [ -f /etc/profile.d/jdk.sh ] && . /etc/profile.d/jdk.sh' \
      -i logstash.lib.sh
}

sha256sums=('8e073ee4c0dd346c62d538d3f28c95e536b9d3f269d0f993cff8662d799163d3'
            '984a972a67e8e16d0c8d81aeb312bf49258052eb8bc5cea340709756190619a8'
            'edc0ff1b4c2ba10c7907c4ed7e93cd035cc9c352b6c00cc514dc14cd3fa686cf'
            '327df7c1a9c5d4c0c21ed2996f6db0831c9956816568e252d952e70355128770')
