# $Id: PKGBUILD 101963 2013-12-03 00:41:07Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Valentin HÄƒloiu <vially.ichb+aur@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kevin Zuber <uKev@knet.eu>
# Contributor: Vsevolod Balashov <vsevolod@balashov.name>

pkgbase=uwsgi-plugins-phpxx
pkgname=(
        uwsgi-plugin-php53
        uwsgi-plugin-php54
        uwsgi-plugin-php55
        )
pkgver=2.0.10
pkgrel=1
arch=(i686 x86_64)
url="http://projects.unbit.it/uwsgi"
license=(GPL2)
conflicts=(python-uwsgi)
makedepends=(gcc python2 curl libxml2 libyaml pcre libedit openssl bzip2 gmp pam jansson php53-embed php54-embed php55-embed uwsgi)
source=(http://projects.unbit.it/downloads/uwsgi-$pkgver.tar.gz
        archlinux.ini
        uwsgi_fix_rpath.patch
        uwsgi_trick_chroot.patch)

prepare(){
    cd "$srcdir"/uwsgi-$pkgver
    cp "$srcdir"/archlinux.ini buildconf/archlinux.ini
    for patch in uwsgi_fix_rpath.patch uwsgi_trick_chroot.patch; do
        patch -Np1 -i "$srcdir"/$patch
    done
}

build() {
  pushd "$srcdir"/uwsgi-$pkgver
  UWSGICONFIG_PHPDIR=/opt/php53 python2 uwsgiconfig.py --plugin plugins/php archlinux php53
  UWSGICONFIG_PHPDIR=/opt/php54 python2 uwsgiconfig.py --plugin plugins/php archlinux php54
  UWSGICONFIG_PHPDIR=/opt/php55 python2 uwsgiconfig.py --plugin plugins/php archlinux php55
}

package_uwsgi-plugin-php53(){
    depends=(uwsgi php53-embed libyaml jansson)
    pkgdesc="Plugin for PHP 5.3 support"
    install -Dm755 uwsgi-$pkgver/php53_plugin.so "$pkgdir"/usr/lib/uwsgi/php53_plugin.so
}

package_uwsgi-plugin-php54(){
    depends=(uwsgi php54-embed libyaml jansson)
    pkgdesc="Plugin for PHP 5.4 support"
    install -Dm755 uwsgi-$pkgver/php54_plugin.so "$pkgdir"/usr/lib/uwsgi/php54_plugin.so
}

package_uwsgi-plugin-php55(){
    depends=(uwsgi php55-embed libyaml jansson)
    pkgdesc="Plugin for PHP 5.5 support"
    install -Dm755 uwsgi-$pkgver/php55_plugin.so "$pkgdir"/usr/lib/uwsgi/php55_plugin.so
}

sha256sums=('c0b381d6c22da931e85e3efe612629fe33a01ac25b0f028aa631b85d86d5028b'
            '74d924d2509b1a0e7893f7e7a65c95c0abb8da8bbd7523d56ee5441635826a84'
            '9111425606330c6909bc5f8fe24208267358b6cf7fee4a23d0ec677fd1b51e49'
            '43f7244b3372b4f43f88adfd6564960a1e08cdc1007f578e66262222d6b174f4')
