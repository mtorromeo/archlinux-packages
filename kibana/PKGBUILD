# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=kibana
pkgver=5.2.2
pkgrel=1
pkgdesc="browser based analytics and search dashboard for Elasticsearch. Please note; this package replaces the distributed precompiled binary 'node'"
arch=('any')
url="https://www.elastic.co/products/kibana"
license=('apache')
depends=('nodejs')
optdepends=('elasticsearch')
backup=('etc/kibana/kibana.yml')
options=('!strip')
source=("https://artifacts.elastic.co/downloads/$pkgname/$pkgname-$pkgver-linux-x86_64.tar.gz"
        kibana.service
        kibana.tmpfile
        kibana.user)
sha256sums=('9c7c526ce286da7f63aed9fb9f9d752ec182ff16bf374c55e55e5a0536a33563'
            '60a6f7931356d2d64d080e270b513c827e04cd9f25109eb0c1abe31a57a83abe'
            '03c35273ad89f4a21eb5466c3f30c0d7b75886bb305996002c1846fb5f7a078b'
            '7df2fc315461da233fc29dc0ee3df7a66e291013bd0d38716d668c69b4ad8f7d')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver-linux-x86_64
  rm -rf node data

  find node_modules -type d -name .bin | while read bindir; do
    cd "$srcdir"/$pkgname-$pkgver-linux-x86_64/$bindir

    # replace hardlinks with symlinks
    find -type f -links +1 -printf "%i %p\n" | \
    while read inode1 linkname; do
      find .. ! -path ../.bin/\* -type f -links +1 -printf "%i %p\n" | sort -nk1 | \
      while read inode2 file; do
        if [[ $inode1 == $inode2 ]]; then
          rm -f "$linkname"
          ln -vs "$file" "$linkname"
        fi
      done
    done
  done

}

package() {
  cd "$pkgdir"

  install -dm755 usr/share
  install -Dm644 "$srcdir"/kibana.service usr/lib/systemd/system/kibana.service
  install -Dm644 "$srcdir"/kibana.tmpfile usr/lib/tmpfiles.d/kibana.conf
  install -Dm644 "$srcdir"/kibana.user usr/lib/sysusers.d/kibana.conf

  cd "$srcdir"/$pkgname-$pkgver-linux-x86_64
  install -Dm644 config/kibana.yml "$pkgdir"/etc/kibana/kibana.yml

  cd "$srcdir"
  mv $pkgname-$pkgver-linux-x86_64 "$pkgdir"/usr/share/kibana
  ln -s /var/lib/kibana "$pkgdir"/usr/share/kibana/data
}
