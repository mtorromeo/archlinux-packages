# Maintainer: Yuval Adam <yuval at y3xz dot com> PGP-Key: 271386AA2EB7672F
# Contributor: Kenny Rasschaert <kenny dot rasschaert at gmail dot com> PGP-Key: 1F70454121E41419
# Contributor: Adrián Pérez de Castro <adrian at perezdecastro dor org> PGP-Key: 91C559DBE4C9123B
# Contributor: Carl George <arch at cgtx dot us> PGP-Key: 4BA2F7E101D9F512

pkgname=rkt
pkgver=1.0.0
pkgrel=3
pkgdesc="App container runtime"
arch=('x86_64')
url="https://github.com/coreos/rkt"
license=(apache)
depends=('glibc' 'openssl' 'zlib')
makedepends=('cpio' 'go' 'wget' 'squashfs-tools' 'perl-capture-tiny'
             'intltool' 'gperf' 'git' 'libseccomp' 'bc' 'glibc-static')
provides=('rkt')
replaces=('rocket')
conflicts=('rocket')
source=("https://github.com/coreos/rkt/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "rkt.sysusers")
sha256sums=('7e30b03fa51a34db095484c4b111c526e49bead4e33232e1b239090503d7ac97'
            '1ad8d343191be731289577d249a2467fbe5a69949117601e760b459f599d311f')
install="$pkgname.install"

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-tpm=auto \
    --with-stage1-flavors=coreos,kvm,host,fly \
    --with-stage1-default-location=/usr/lib/rkt/stage1-coreos.aci
  make all
}

package() {
  cd $pkgname-$pkgver

  for unit in rkt-gc.{timer,service} rkt-metadata.{socket,service}; do
    install -Dm644 dist/init/systemd/$unit "$pkgdir"/usr/lib/systemd/system/$unit
  done

  install -Dm644 dist/init/systemd/tmpfiles.d/rkt.conf "$pkgdir"/usr/lib/tmpfiles.d/rkt.conf
  install -Dm644 "$srcdir"/rkt.sysusers "$pkgdir"/usr/lib/sysusers.d/rkt.conf
  install -Dm644 dist/bash_completion/rkt.bash "$pkgdir"/usr/share/bash-completion/completions/rkt

  cd build-$pkgname-$pkgver
  install -dm755 "$pkgdir"/usr/bin "$pkgdir"/usr/lib/rkt/stage1-images
  install -dm775 "$pkgdir"/var/lib/rkt
  mv bin/{rkt,actool} "$pkgdir"/usr/bin
  mv bin/stage1-*.aci "$pkgdir"/usr/lib/rkt/
}

# vim:set ts=2 sw=2 et:
